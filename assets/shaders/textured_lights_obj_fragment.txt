#version 460 core

uniform vec3 DIFFUSE;
uniform vec3 SPECULAR;
uniform sampler2D diffuseTexture;

uniform vec3 viewPos;
uniform float shininess;

uniform bool useAmbient;
uniform vec3 ambientColor;
uniform float ambientIntensity;

uniform int numDirectionalLights;
uniform vec3 directionalDir[4];
uniform vec3 directionalColor[4];
uniform float directionalIntensity[4];

uniform int numSpotLights;
uniform vec3 spotPos[4];
uniform vec3 spotDir[4];
uniform vec3 spotColor[4];
uniform float spotIntensity[4];
uniform float spotCutOff[4];
uniform float spotOuterCutOff[4];
uniform float spotConstant[4];
uniform float spotLinear[4];
uniform float spotQuadratic[4];

in vec3 fragNormal;
in vec3 fragPosition;
in vec2 fragTexCoords;
in vec3 fragWorldPosition;

out vec4 fragColor;

void main() {
    vec3 texColor = texture(diffuseTexture, fragTexCoords).rgb;
    vec3 result = vec3(0.0);
    vec3 norm = normalize(fragNormal);
    vec3 viewDir = normalize(viewPos - fragWorldPosition);

    if (useAmbient) {
        result += ambientColor * ambientIntensity;
    }

    for (int i = 0; i < numDirectionalLights; i++) {
        vec3 lightDir = normalize(-directionalDir[i]);
        float diff = max(dot(norm, lightDir), 0.0);
        vec3 diffuse = diff * directionalColor[i] * directionalIntensity[i];
        result += diffuse;

        vec3 reflectDir = reflect(-lightDir, norm);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
        vec3 specular = spec * SPECULAR * directionalIntensity[i];
        result += specular;
    }

    for (int i = 0; i < numSpotLights; i++) {
        vec3 lightDir = normalize(spotPos[i] - fragWorldPosition);
        float theta = dot(lightDir, normalize(-spotDir[i]));
        float epsilon = spotCutOff[i] - spotOuterCutOff[i];
        float intensity = clamp((theta - spotOuterCutOff[i]) / epsilon, 0.0, 1.0);

        float distance = length(spotPos[i] - fragWorldPosition);
        float attenuation = 1.0 / (spotConstant[i] + spotLinear[i] * distance + spotQuadratic[i] * distance * distance);

        float diff = max(dot(norm, lightDir), 0.0);
        vec3 diffuse = diff * spotColor[i] * spotIntensity[i] * attenuation * intensity;
        result += diffuse;

        vec3 reflectDir = reflect(-lightDir, norm);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
        vec3 specular = spec * SPECULAR * spotIntensity[i] * attenuation * intensity;
        result += specular;
    }

    fragColor = vec4(texColor * result, 1.0);
}
