#version 460 core

uniform vec3 DIFFUSE;
uniform vec3 SPECULAR;
uniform sampler2D diffuseTexture;

uniform vec3 viewPos;
uniform float shininess;

uniform bool useAmbient;
uniform vec3 ambientColor;
uniform float ambientIntensity;

uniform bool useDirectional;
uniform vec3 directionalDir;
uniform vec3 directionalColor;
uniform float directionalIntensity;

uniform bool useSpot;
uniform vec3 spotPos;
uniform vec3 spotDir;
uniform vec3 spotColor;
uniform float spotIntensity;
uniform float spotCutOff;
uniform float spotOuterCutOff;
uniform float spotConstant;
uniform float spotLinear;
uniform float spotQuadratic;

in vec3 fragNormal;
in vec3 fragPosition;
in vec2 fragTexCoords;
in vec3 fragWorldPosition;

out vec4 fragColor;

void main() {
    vec3 texColor = texture(diffuseTexture, fragTexCoords).rgb;
    vec3 result = vec3(0.0);
    vec3 norm = normalize(fragNormal);
    vec3 viewDir = normalize(viewPos - fragWorldPosition);

    if (useAmbient) {
        result += ambientColor * ambientIntensity;
    }

    if (useDirectional) {
        vec3 lightDir = normalize(-directionalDir);
        float diff = max(dot(norm, lightDir), 0.0);
        vec3 diffuse = diff * directionalColor * directionalIntensity;
        result += diffuse;

        vec3 reflectDir = reflect(-lightDir, norm);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
        vec3 specular = spec * SPECULAR * directionalIntensity;
        result += specular;
    }

    if (useSpot) {
        vec3 lightDir = normalize(spotPos - fragWorldPosition);
        float theta = dot(lightDir, normalize(-spotDir));
        float epsilon = spotCutOff - spotOuterCutOff;
        float intensity = clamp((theta - spotOuterCutOff) / epsilon, 0.0, 1.0);

        float distance = length(spotPos - fragWorldPosition);
        float attenuation = 1.0 / (spotConstant + spotLinear * distance + spotQuadratic * distance * distance);

        float diff = max(dot(norm, lightDir), 0.0);
        vec3 diffuse = diff * spotColor * spotIntensity * attenuation * intensity;
        result += diffuse;

        vec3 reflectDir = reflect(-lightDir, norm);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
        vec3 specular = spec * SPECULAR * spotIntensity * attenuation * intensity;
        result += specular;
    }

    fragColor = vec4(texColor * result, 1.0);
}
