#version 460 core

uniform vec3 DIFFUSE;
uniform vec3 SPECULAR;
uniform sampler2D diffuseTexture;
uniform sampler2D shadowTexture;

uniform vec3 viewPos;
uniform float shininess;

uniform bool useAmbient;
uniform vec3 ambientColor;
uniform float ambientIntensity;

uniform int numDirectionalLights;
uniform vec3 directionalDir[4];
uniform vec3 directionalColor[4];
uniform float directionalIntensity[4];

uniform int numSpotLights;
uniform vec3 spotPos[4];
uniform vec3 spotDir[4];
uniform vec3 spotColor[4];
uniform float spotIntensity[4];
uniform float spotCutOff[4];
uniform float spotOuterCutOff[4];
uniform float spotConstant[4];
uniform float spotLinear[4];
uniform float spotQuadratic[4];

uniform mat4 lightSpaceMatrix;

in vec3 fragNormal;
in vec3 fragPosition;
in vec2 fragTexCoords;
in vec3 fragWorldPosition;

out vec4 fragColor;

float CalculateShadow(vec4 fragPosLS, vec3 normal, vec3 lightDir) {
    vec3 projCoords = fragPosLS.xyz / fragPosLS.w;
    projCoords = projCoords * 0.5 + 0.5;

    if(projCoords.z > 1.0) return 0.0;

    float currentDepth = projCoords.z;
    float bias = max(0.05 * (1.0 - dot(normal, lightDir)), 0.005);
    
    float shadow = 0.0;
    vec2 texelSize = 1.0 / textureSize(shadowTexture, 0);
    for(int x = -1; x <= 1; ++x) {
        for(int y = -1; y <= 1; ++y) {
            float pcfDepth = texture(shadowTexture, projCoords.xy + vec2(x, y) * texelSize).r; 
            shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;        
        }    
    }
    return shadow / 9.0;
}

void main() {
    vec3 texColor = texture(diffuseTexture, fragTexCoords).rgb;
    vec3 norm = normalize(fragNormal);
    vec3 viewDir = normalize(viewPos - fragWorldPosition);

    vec4 fragPosLightSpace = lightSpaceMatrix * vec4(fragWorldPosition, 1.0);

    vec3 ambient = vec3(0.0);
    if (useAmbient) {
        ambient = ambientColor * ambientIntensity;
    }

    vec3 lighting = vec3(0.0);

    for (int i = 0; i < numDirectionalLights; i++) {
        vec3 lightDir = normalize(-directionalDir[i]);
        
        float shadow = (i == 0) ? CalculateShadow(fragPosLightSpace, norm, lightDir) : 0.0;
        float shadowFactor = 1.0 - shadow;

        float diff = max(dot(norm, lightDir), 0.0);
        vec3 diffuse = diff * directionalColor[i] * directionalIntensity[i];

        vec3 reflectDir = reflect(-lightDir, norm);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
        vec3 specular = spec * SPECULAR * directionalIntensity[i];
        
        lighting += (diffuse + specular) * shadowFactor;
    }

    for (int i = 0; i < numSpotLights; i++) {
        vec3 lightDir = normalize(spotPos[i] - fragWorldPosition);
        
        float shadow = (i == 0) ? CalculateShadow(fragPosLightSpace, norm, lightDir) : 0.0;
        float shadowFactor = 1.0 - shadow;

        float theta = dot(lightDir, normalize(-spotDir[i]));
        float epsilon = spotCutOff[i] - spotOuterCutOff[i];
        float intensity = clamp((theta - spotOuterCutOff[i]) / epsilon, 0.0, 1.0);

        float distance = length(spotPos[i] - fragWorldPosition);
        float attenuation = 1.0 / (spotConstant[i] + spotLinear[i] * distance + spotQuadratic[i] * distance * distance);

        float diff = max(dot(norm, lightDir), 0.0);
        vec3 diffuse = diff * spotColor[i] * spotIntensity[i] * attenuation * intensity;

        vec3 reflectDir = reflect(-lightDir, norm);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
        vec3 specular = spec * SPECULAR * spotIntensity[i] * attenuation * intensity;
        
        lighting += (diffuse + specular) * shadowFactor;
    }

    fragColor = vec4(texColor * (ambient + lighting), 1.0);
}