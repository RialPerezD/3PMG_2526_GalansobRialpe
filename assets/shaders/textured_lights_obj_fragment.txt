#version 460 core

uniform vec3 DIFFUSE;
uniform vec3 SPECULAR;
uniform sampler2D diffuseTexture;
uniform sampler2D shadowTexture;

uniform vec3 viewPos;
uniform float shininess;

uniform bool useAmbient;
uniform vec3 ambientColor;
uniform float ambientIntensity;

uniform int lightType; // 0 = none, 1 = directional, 2 = spot
uniform vec3 lightDirOrPos;
uniform vec3 spotLightDir;
uniform vec3 lightColor;
uniform float lightIntensity;

uniform float spotCutOff;
uniform float spotOuterCutOff;
uniform float spotConstant;
uniform float spotLinear;
uniform float spotQuadratic;

uniform mat4 lightSpaceMatrix;

in vec3 fragNormal;
in vec3 fragPosition;
in vec2 fragTexCoords;
in vec3 fragWorldPosition;

out vec4 fragColor;

float CalculateShadow(vec4 fragPosLS, vec3 normal, vec3 lightDir) {
    vec3 projCoords = fragPosLS.xyz / fragPosLS.w;
    projCoords = projCoords * 0.5 + 0.5;

    if(projCoords.z > 1.0) return 0.0;

    if(projCoords.x < 0.0 || projCoords.x > 1.0 || projCoords.y < 0.0 || projCoords.y > 1.0) return 0.0;

    float currentDepth = projCoords.z;
    float bias = max(0.05 * (1.0 - dot(normal, lightDir)), 0.0005);
    
    float shadow = 0.0;
    vec2 texelSize = 1.0 / textureSize(shadowTexture, 0);
    for(int x = -1; x <= 1; ++x) {
        for(int y = -1; y <= 1; ++y) {
            vec2 coords = projCoords.xy + vec2(x, y) * texelSize;
            vec2 normalizedCoords = clamp(coords, 0.0, 1.0);

            float pcfDepth = texture(shadowTexture, normalizedCoords).r; 
            shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;        
        }    
    }
    return shadow / 9.0;
}

void main() {
    vec3 texColor = texture(diffuseTexture, fragTexCoords).rgb;
    vec3 norm = normalize(fragNormal);
    vec3 viewDir = normalize(viewPos - fragWorldPosition);

    vec4 fragPosLightSpace = lightSpaceMatrix * vec4(fragWorldPosition, 1.0);

    vec3 ambient = vec3(0.0);
    if (useAmbient) {
        ambient = ambientColor * ambientIntensity;
    }

    vec3 lighting = vec3(0.0);

    if (lightType == 1) {
        vec3 lightDirection = normalize(-lightDirOrPos);
        
        float shadow = CalculateShadow(fragPosLightSpace, norm, lightDirection);
        float shadowFactor = 1.0 - shadow;

        float diff = max(dot(norm, lightDirection), 0.0);
        vec3 diffuse = diff * lightColor * lightIntensity;

        vec3 reflectDir = reflect(-lightDirection, norm);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
        vec3 specular = spec * SPECULAR * lightIntensity;
        
        lighting += (diffuse + specular) * shadowFactor;
    }
    else if (lightType == 2) {
        vec3 lightDir = normalize(lightDirOrPos - fragWorldPosition);
        
        float shadow = CalculateShadow(fragPosLightSpace, norm, lightDir);
        float shadowFactor = 1.0 - shadow;

        float theta = dot(lightDir, normalize(-spotLightDir));
        float epsilon = spotCutOff - spotOuterCutOff;
        float intensity = clamp((theta - spotOuterCutOff) / epsilon, 0.0, 1.0);

        float distance = length(lightDirOrPos - fragWorldPosition);
        float attenuation = 1.0 / (spotConstant + spotLinear * distance + spotQuadratic * distance * distance);

        float diff = max(dot(norm, lightDir), 0.0);
        vec3 diffuse = diff * lightColor * lightIntensity * attenuation * intensity;

        vec3 reflectDir = reflect(-lightDir, norm);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
        vec3 specular = spec * SPECULAR * lightIntensity * attenuation * intensity;
        
        lighting += (diffuse + specular) * shadowFactor;
    }

    fragColor = vec4(texColor * (ambient + lighting), 1.0);
}
